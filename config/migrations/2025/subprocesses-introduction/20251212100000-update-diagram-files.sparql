PREFIX mu:<http://mu.semte.ch/vocabularies/core/>
PREFIX nfo:<http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX nie:<http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dpv:<https://w3id.org/dpv#>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX schema: <http://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>

INSERT {
  GRAPH ?g {
    ?process schema:hasPart ?list .

    ?list a schema:ItemList ;
      mu:uuid ?listUuid ;
      schema:itemListElement ?item ;
      schema:itemListOrder schema:ItemListUnordered ;
      dct:created ?created ;
      dct:modified ?modified .

    ?item a schema:ListItem ;
      mu:uuid ?itemUuid ;
      schema:item ?diagramFile ;
      dct:created ?created ;
      dct:modified ?modified .
  }
}
WHERE {
  GRAPH ?g {
    ?diagramFile a nfo:FileDataObject ;
      dbpedia:fileExtension ?ext ;
      nie:isPartOf ?process ;
      dct:created ?created ;
      dct:modified ?modified .
    ?process a dpv:Process .
  }

  FILTER(LCASE(STR(?ext)) IN ("bpmn", "vsdx"))

  BIND(STRUUID() AS ?listUuid)
  BIND(STRUUID() AS ?itemUuid)

  BIND(IRI(CONCAT("http://data.lblod.info/diagram-lists/", ?listUuid)) AS ?list)
  BIND(IRI(CONCAT("http://data.lblod.info/list-items/", ?itemUuid)) AS ?item)
}
